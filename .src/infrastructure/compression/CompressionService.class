' Gambas class file

'
'
' Clase de manejo de compresion de archivos
'
'
Private $log4gambas As Log4Gambas3
Private $configuration As Configuration
Private $titulo As String = "[CompressionService] "

' ==================================================
'
'          Metodos publicos
'
' ==================================================
Public Sub _new(configuration As Configuration, logger As Log4Gambas3)

  $configuration = configuration
  $log4gambas = logger

End

Public Function SaveImageIO(originalImage As String) As String

  Dim filename As String = File.BaseName(originalImage) & "." & File.Ext(originalImage)
  Dim newPath As String = $configuration.LibraryPath &/ Format(Now, "yyyymmdd")

  If Not Exist(newPath) Then
    Mkdir newPath
  Endif

  Copy originalImage To newPath &/ filename

  Return newPath

Catch
  $log4gambas.Error($titulo & "Problema al intentar grabar la foto " & filename)
  Error.Propagate

End

Public Function Hash256Calculate(originalImage As String) As String

  Dim hash256 As String

  hash256 = Hash.Sha256(originalImage)

  Return hash256

Catch
  $log4gambas.Error($titulo & "Problema al calcular hash256 de la foto " & originalImage)
  Error.Propagate

End

Public Function ValidateFileExist(filename As String) As Boolean

  If Exist($configuration.LibraryPath &/ Format(Now, "yyyymmdd") &/ filename) Then
    Return True
  Endif

  Return False

Catch
  $log4gambas.Error($titulo & "Problema al valida existencia del archivo " & filename)
  Error.Propagate

End

Public Function CreateThumbnail(originalPhoto As Image, imagenPath As String) As String

  Dim thumbnailImage As Image
  Dim tempThumbFile As String
  Dim thumbnailString As String = ""
  Dim THUMBNAIL_MIN_BENEFIT As Float = 1.5      ' Generar si reduce al menos 50%
  ' TODO: Crear un calculo para determinar cuando hacer una miniatura

  $log4gambas.Debug($titulo & "Tama√±o de la imagen " & originalPhoto.Width & " x " & originalPhoto.Height)

  If originalPhoto.Width <= 255 Or originalPhoto.Height <= 255 Then
    $log4gambas.Debug($titulo & "imagen es menor que rango aceptable va sin miniatura")
  Else
    thumbnailImage = originalPhoto.Stretch(320, -1)
    tempThumbFile = Temp() & "." & File.Ext(imagenPath)
    thumbnailString = thumbnailImage.ToString(File.Ext(imagenPath), 100)
    File.Save(tempThumbFile, thumbnailString)
    thumbnailString = File.Load(tempThumbFile)
  Endif
  Try Kill tempThumbFile

  Return thumbnailString

Catch
  $log4gambas.Error($titulo & "Problema al crear la miniatura de " & File.BaseName(imagenPath) & "." & File.Ext(imagenPath))
  Error.Propagate

End
